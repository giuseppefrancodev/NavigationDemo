import org.jlleitschuh.gradle.ktlint.reporter.ReporterType

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'dagger.hilt.android.plugin'
    id 'com.google.devtools.ksp'
    id 'org.jlleitschuh.gradle.ktlint'
    id 'org.jetbrains.kotlin.plugin.compose'
}

android {
    namespace 'com.example.navigation'
    compileSdk 35

    defaultConfig {
        applicationId "com.example.navigation"
        minSdk 26
        targetSdk 35
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        externalNativeBuild {
            cmake {
                cppFlags "-std=c++17"
                arguments "-DANDROID_STL=c++_shared"
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion kotlin_version
    }

    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
        }
    }

    ktlint {
        android.set(true)
        reporters {
            reporter(ReporterType.PLAIN)
            reporter(ReporterType.CHECKSTYLE)
        }
        filter {
            exclude("**/generated/**")
            include("**/kotlin/**")
        }
    }
}

ext {
    core_ktx_version = '1.15.0'
    lifecycle_version = '2.8.7'
    activity_compose_version = '1.10.1'
    material_version = '1.12.0'
    compose_bom_version = '2025.02.00'
    material3_version = '1.3.1'
    material2_version = '1.7.8'
    maps_compose_version = '2.15.0'
    play_services_maps_version = '19.1.0'
    play_services_location_version = '21.3.0'
    coroutines_version = '1.8.0'
    hilt_navigation_compose_version = '1.2.0'
    junit_version = '4.13.2'
    androidx_test_junit_version = '1.2.1'
    espresso_version = '3.6.1'
    mockito_core_version = '4.5.1'
    mockito_kotlin_version = '4.0.0'
    mockito_android_version = '4.5.0'
}

dependencies {
    // Core Android
    implementation "androidx.core:core-ktx:$core_ktx_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.activity:activity-compose:$activity_compose_version"

    // Material Components (non-Compose)
    implementation "com.google.android.material:material:$material_version"

    // Compose
    implementation platform("androidx.compose:compose-bom:$compose_bom_version")
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.ui:ui-tooling-preview'

    // Material 3
    implementation "androidx.compose.material3:material3:$material3_version"
    implementation "androidx.compose.material3:material3-window-size-class:$material3_version"

    // Also include Material 2 for backward compatibility
    implementation "androidx.compose.material:material:$material2_version"

    implementation 'androidx.compose.runtime:runtime-livedata'

    // Maps
    implementation "com.google.maps.android:maps-compose:$maps_compose_version"
    implementation "com.google.android.gms:play-services-maps:$play_services_maps_version"
    implementation "com.google.android.gms:play-services-location:$play_services_location_version"

    // Lifecycle & ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycle_version"

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    // Hilt with KSP
    implementation "com.google.dagger:hilt-android:$hilt_version"
    ksp "com.google.dagger:hilt-compiler:$hilt_version"
    implementation "androidx.hilt:hilt-navigation-compose:$hilt_navigation_compose_version"

    // Testing
    testImplementation "junit:junit:$junit_version"
    androidTestImplementation "androidx.test.ext:junit:$androidx_test_junit_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"
    androidTestImplementation platform("androidx.compose:compose-bom:$compose_bom_version")
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"
    androidTestImplementation "com.google.dagger:hilt-android-testing:$hilt_version"
    kspAndroidTest "com.google.dagger:hilt-android-compiler:$hilt_version"

    // Debug
    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'
}
